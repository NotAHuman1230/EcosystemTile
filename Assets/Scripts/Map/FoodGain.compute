#pragma kernel FoodUpdate
#pragma kernel FoodInitialisation

int2 resolution;
float seed;
float2 foodGain;
float2 foodRange;
Texture2D<float4> water;
Texture2D<float4> desert;
Texture2D<float4> food;
RWTexture2D<float4> result;

float Random(float2 uv)
{
    return frac(sin(dot(uv, float2(12.9898, 78.233))) * 43758.5453123);
}

[numthreads(8,8,1)]
void FoodUpdate (uint3 id : SV_DispatchThreadID)
{
    float2 uv = id.xy / ((float2) resolution.xy - float2(1.0, 1.0));
    if (uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0)
        return;
    
    if(water[id.xy].x >= 1.0)
        return;
    
    float desertValue = clamp(desert[id.xy].x, 0.0, 1.0);
    float foodGainValue = lerp(foodGain.x, foodGain.y, desertValue); 
    float foodRangeValue = lerp(foodRange.x, foodRange.y, desertValue);
    
    float foodValue = clamp(food[id.xy].x + foodGainValue, 0.0, foodRangeValue);
    result[id.xy] = float4(foodValue, foodValue, foodValue, 1.0);
}

[numthreads(8, 8, 1)]
void FoodInitialisation(uint3 id : SV_DispatchThreadID)
{
    float2 uv = id.xy / ((float2) resolution.xy - float2(1.0, 1.0));
    if (uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0)
        return;
    
    if (water[id.xy].x >= 1.0)
    {
        result[id.xy] = float4(0.0, 0.0, 0.0, 1.0);
        return;
    }
    
    float desertValue = clamp(desert[id.xy].x, 0.0, 1.0);
    float foodValue = lerp(foodRange.x, foodRange.y, desertValue);
    foodValue *= Random(uv + seed);
    result[id.xy] = float4(foodValue, foodValue, foodValue, 1);

}